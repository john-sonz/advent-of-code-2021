<!-- livebook:{"persist_outputs":true} -->

# Day 14: Extended Polymerization

## Part 1

The incredible pressures at this depth are starting to put a strain on your submarine. The submarine has polymerization equipment that would produce suitable materials to reinforce the submarine, and the nearby volcanically-active caves should even have the necessary input elements in sufficient quantities.

The submarine manual contains instructions for finding the optimal polymer formula; specifically, it offers a polymer template and a list of pair insertion rules (your puzzle input). You just need to work out what polymer would result after repeating the pair insertion process a few times.

For example:

```
NNCB

CH -> B
HH -> N
CB -> H
NH -> C
HB -> C
HC -> B
HN -> C
NN -> C
BH -> H
NC -> B
NB -> B
BN -> B
BB -> N
BC -> B
CC -> N
CN -> C
```

The first line is the polymer template - this is the starting point of the process.

The following section defines the pair insertion rules. A rule like AB -> C means that when elements A and B are immediately adjacent, element C should be inserted between them. These insertions all happen simultaneously.

So, starting with the polymer template NNCB, the first step simultaneously considers all three pairs:

* The first pair (NN) matches the rule NN -> C, so element C is inserted between the first N and the second N.
* The second pair (NC) matches the rule NC -> B, so element B is inserted between the N and the C.
* The third pair (CB) matches the rule CB -> H, so element H is inserted between the C and the B.

Note that these pairs overlap: the second element of one pair is the first element of the next pair. Also, because all pairs are considered simultaneously, inserted elements are not considered to be part of a pair until the next step.

After the first step of this process, the polymer becomes NCNBCHB.

Here are the results of a few steps using the above rules:

```
Template:     NNCB
After step 1: NCNBCHB
After step 2: NBCCNBBBCBHCB
After step 3: NBBBCNCCNBBNBNBBCHBHHBCHB
After step 4: NBBNBNBBCCNBCNCCNBBNBBNBBBNBBNBBCBHCBHHNHCBBCBHCB
```

This polymer grows quickly. After step 5, it has length 97; After step 10, it has length 3073. After step 10, B occurs 1749 times, C occurs 298 times, H occurs 161 times, and N occurs 865 times; taking the quantity of the most common element (B, 1749) and subtracting the quantity of the least common element (H, 161) produces 1749 - 161 = 1588.

Apply 10 steps of pair insertion to the polymer template and find the most and least common elements in the result. What do you get if you take the quantity of the most common element and subtract the quantity of the least common element?

```elixir
File.cd!("day14")
```

```output
:ok
```

```elixir
[template, rules_string] =
  File.read!("input.txt")
  |> String.split("\n\n")

rules =
  rules_string
  |> String.split("\n", trim: true)
  |> Enum.map(&String.split(&1, " -> "))
  |> Enum.into(%{}, &List.to_tuple/1)

template_frequencies =
  template
  |> String.graphemes()
  |> Enum.chunk_every(2, 1, :discard)
  |> Enum.map(&Enum.join/1)
  |> Enum.frequencies()

{template, template_frequencies, rules}
```

```output
{"NBOKHVHOSVKSSBSVVBCS",
 %{
   "BC" => 1,
   "BO" => 1,
   "BS" => 1,
   "CS" => 1,
   "HO" => 1,
   "HV" => 1,
   "KH" => 1,
   "KS" => 1,
   "NB" => 1,
   "OK" => 1,
   "OS" => 1,
   "SB" => 1,
   "SS" => 1,
   "SV" => 2,
   "VB" => 1,
   "VH" => 1,
   "VK" => 1,
   "VV" => 1
 },
 %{
   "HV" => "P",
   "CB" => "H",
   "PO" => "P",
   "PV" => "V",
   "HP" => "S",
   "KC" => "F",
   "HK" => "N",
   "ON" => "S",
   "VF" => "C",
   "CP" => "V",
   "OO" => "P",
   "VV" => "B",
   "FO" => "C",
   "NB" => "K",
   "KH" => "B",
   "CN" => "H",
   "SO" => "H",
   "BS" => "P",
   "HN" => "C",
   "KB" => "N",
   "CC" => "S",
   "FF" => "C",
   "CH" => "C",
   "NF" => "P",
   "SS" => "P",
   "FP" => "K",
   "PB" => "K",
   "KN" => "C",
   "SF" => "K",
   "OK" => "V",
   "FB" => "K",
   "BO" => "B",
   "OF" => "F",
   "VO" => "V",
   "KP" => "O",
   "FC" => "B",
   "OB" => "N",
   "OC" => "K",
   "CS" => "P",
   "FN" => "P",
   "SH" => "H",
   "FK" => "F",
   "NO" => "C",
   "HF" => "F",
   "CK" => "P",
   "FH" => "O",
   "HC" => "S",
   ...
 }}
```

```elixir
defmodule Polymer do
  def step(pairs_counts, rules) do
    pairs_counts
    |> Enum.reduce(%{}, fn {pair, count}, acc ->
      [l1, l2] = String.graphemes(pair)

      case rules[pair] do
        nil ->
          acc

        insertion ->
          acc
          |> Map.update(l1 <> insertion, count, &(&1 + count))
          |> Map.update(insertion <> l2, count, &(&1 + count))
      end
    end)
  end

  def get_answer(pairs_counts, template) do
    pairs_counts
    |> Enum.reduce(%{String.at(template, -1) => 1}, fn {pair, count}, acc ->
      Map.update(acc, String.at(pair, 0), count, &(&1 + count))
    end)
    |> Map.values()
    |> Enum.min_max()
    |> then(fn {min, max} -> max - min end)
  end
end
```

```output
{:module, Polymer, <<70, 79, 82, 49, 0, 0, 12, ...>>, {:get_answer, 2}}
```

```elixir

```

```output
nil
```

```elixir
1..10
|> Enum.reduce(template_frequencies, fn _, acc -> Polymer.step(acc, rules) end)
|> Polymer.get_answer(template)
```

```output
3342
```

## Part 2

The resulting polymer isn't nearly strong enough to reinforce the submarine. You'll need to run more steps of the pair insertion process; a total of 40 steps should do it.

In the above example, the most common element is B (occurring 2192039569602 times) and the least common element is H (occurring 3849876073 times); subtracting these produces 2188189693529.

Apply 40 steps of pair insertion to the polymer template and find the most and least common elements in the result. What do you get if you take the quantity of the most common element and subtract the quantity of the least common element?

```elixir
1..40
|> Enum.reduce(template_frequencies, fn _, acc -> Polymer.step(acc, rules) end)
|> Polymer.get_answer(template)
```

```output
3776553567525
```
