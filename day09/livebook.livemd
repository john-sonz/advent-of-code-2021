<!-- vim: set syntax=markdown: -->

<!-- livebook:{"persist_outputs":true} -->

# Day 9: Smoke Basin

## Part 1

These caves seem to be lava tubes. Parts are even still volcanically active; small hydrothermal vents release smoke into the caves that slowly settles like rain.

If you can model how the smoke flows through the caves, you might be able to avoid it and be that much safer. The submarine generates a heightmap of the floor of the nearby caves for you (your puzzle input).

Smoke flows to the lowest point of the area it's in. For example, consider the following heightmap:

```
2199943210
3987894921
9856789892
8767896789
9899965678
```

Each number corresponds to the height of a particular location, where 9 is the highest and 0 is the lowest a location can be.

Your first goal is to find the low points - the locations that are lower than any of its adjacent locations. Most locations have four adjacent locations (up, down, left, and right); locations on the edge or corner of the map have three or two adjacent locations, respectively. (Diagonal locations do not count as adjacent.)

In the above example, there are four low points, all highlighted: two are in the first row (a 1 and a 0), one is in the third row (a 5), and one is in the bottom row (also a 5). All other locations on the heightmap have some lower adjacent location, and so are not low points.

The risk level of a low point is 1 plus its height. In the above example, the risk levels of the low points are 2, 1, 6, and 6. The sum of the risk levels of all low points in the heightmap is therefore 15.

Find all of the low points on your heightmap. What is the sum of the risk levels of all low points on your heightmap?

```elixir
File.cd("day09")
```

```output
:ok
```

```elixir
input =
  File.read!("input.txt")
  |> String.split("\n", trim: true)
  |> Enum.map(fn row -> row |> String.graphemes() |> Enum.map(&String.to_integer/1) end)

heightmap =
  for {row, x} <- Enum.with_index(input), {height, y} <- Enum.with_index(row), into: %{} do
    {{x, y}, height}
  end
```

```output
%{
  {76, 13} => 7,
  {37, 47} => 8,
  {65, 63} => 9,
  {38, 2} => 6,
  {1, 26} => 7,
  {83, 76} => 7,
  {32, 15} => 5,
  {89, 14} => 9,
  {35, 30} => 9,
  {37, 53} => 3,
  {4, 5} => 6,
  {8, 50} => 9,
  {78, 98} => 9,
  {95, 56} => 7,
  {74, 12} => 8,
  {11, 39} => 8,
  {65, 43} => 9,
  {22, 38} => 9,
  {14, 86} => 7,
  {20, 41} => 9,
  {29, 25} => 8,
  {86, 10} => 9,
  {83, 36} => 9,
  {29, 26} => 9,
  {47, 27} => 9,
  {4, 81} => 6,
  {31, 42} => 7,
  {9, 34} => 8,
  {90, 0} => 6,
  {67, 98} => 5,
  {13, 85} => 7,
  {63, 81} => 2,
  {82, 60} => 6,
  {47, 38} => 7,
  {15, 92} => 9,
  {58, 58} => 8,
  {20, 3} => 4,
  {61, 95} => 2,
  {23, 67} => 9,
  {78, 75} => 9,
  {79, 17} => 8,
  {75, 0} => 9,
  {16, 73} => 7,
  {76, 2} => 8,
  {58, 84} => 7,
  {58, 33} => 6,
  {47, 44} => 9,
  {54, 31} => 9,
  {13, ...} => 5,
  {...} => 9,
  ...
}
```

```elixir
get_neighbours = fn {x, y} ->
  [
    {x, y - 1},
    {x, y + 1},
    {x - 1, y},
    {x + 1, y}
  ]
end

low_points =
  heightmap
  |> Map.keys()
  |> Enum.filter(fn {x, y} ->
    lowest_neigbour =
      {x, y}
      |> get_neighbours.()
      |> Enum.map(&Map.get(heightmap, &1))
      |> Enum.reject(&is_nil/1)
      |> Enum.min()

    heightmap[{x, y}] < lowest_neigbour
  end)

low_points
|> Enum.map(&(Map.fetch!(heightmap, &1) + 1))
|> Enum.sum()
```

```output
577
```

## Part 2

Next, you need to find the largest basins so you know what areas are most important to avoid.

A basin is all locations that eventually flow downward to a single low point. Therefore, every low point has a basin, although some basins are very small. Locations of height 9 do not count as being in any basin, and all other locations will always be part of exactly one basin.

The size of a basin is the number of locations within the basin, including the low point. The example above has four basins.

The top-left basin, size 3:

```
2199943210
3987894921
9856789892
8767896789
9899965678
```

The top-right basin, size 9:

```
2199943210
3987894921
9856789892
8767896789
9899965678
```

The middle basin, size 14:

```
2199943210
3987894921
9856789892
8767896789
9899965678
```

The bottom-right basin, size 9:

```
2199943210
3987894921
9856789892
8767896789
9899965678
```

Find the three largest basins and multiply their sizes together. In the above example, this is 9 * 14 * 9 = 1134.

What do you get if you multiply together the sizes of the three largest basins?

```elixir
defmodule Basin do
  def get_neighbours({x, y}) do
    [
      {x, y - 1},
      {x, y + 1},
      {x - 1, y},
      {x + 1, y}
    ]
  end

  def size({x, y}, map) do
    do_size(map, MapSet.new(), MapSet.new([{x, y}]))
  end

  defp do_size(map, seen, to_see) do
    seen = MapSet.union(seen, to_see)

    MapSet.to_list(to_see)
    |> Enum.flat_map(&get_neighbours/1)
    |> Enum.filter(&(Map.get(map, &1, 9) != 9))
    |> MapSet.new()
    |> MapSet.difference(seen)
    |> then(fn to_see ->
      if MapSet.size(to_see) > 0 do
        do_size(map, seen, to_see)
      else
        MapSet.size(seen)
      end
    end)
  end
end

low_points
|> Enum.map(&Basin.size(&1, heightmap))
|> Enum.sort(:desc)
|> Enum.take(3)
|> Enum.reduce(1, &Kernel.*/2)
```

```output
1069200
```
